apply plugin: "java"
apply plugin: "war"
apply plugin: "eclipse-wtp"
apply plugin: "jetty"
apply plugin: "deliveryResources"

version = "1.0"

repositories { mavenCentral() }

ext {
	versionH2db = "1.4.181"
	versionFlyway = "3.0"
	versionJodaTime = "2.4"
	versionGuava = "18.0"
	versionCommonsPool = "1.6"
	versionCommonsDbcp2 = "2.0.1"
	versionCommonsLang3 = "3.3.2"
	versionCommonsMath3 = "3.3"
	versionCommonsCollections4 = "4.0"
	versionCommonsCodec = "1.9"
	versionVelocity = "1.7"
	versionPoi = "3.11"
	versionIcu4j = "54.1.1"
	versionFluentLogger = "0.3.0"
	versionSlf4j = "1.7.7"
	versionLogback = "1.1.2"
	versionMyBatis = "3.2.8"
	versionMyBatisSpring = "1.2.2"
	versionQuerydsl = "3.6.0"
	versionJackson = "2.4.4"
	versionAspectJ = "1.8.2"
	versionAopalliance = "1.0"
	versionSpring = "4.1.3.RELEASE"
	versionSpringSecurity = "3.2.5.RELEASE"
	versionSpringMobile = "1.1.3.RELEASE"
	versionSpringDataJdbcCore = "1.1.0.RELEASE"
	versionHibernateValidator = "5.1.2.Final"
	versionTiles = "3.0.4"
	versionJstl = "1.2"
	versionJavaMail = "1.4.7"
	versionJavaValidation = "1.1.0.Final"
	versionJavaEl = "3.0.0"
	versionJavaServlet = "3.0.1"
	versionJavaJsp = "2.2.1"
	versionJavaJms = "1.1-rev-1"
	versionJavaTransaction = "1.1-rev-1"
	versionLombok = "1.14.8"
	versionJunit = "4.11"
	versionHamcrest = "1.3"
	versionMockito = "1.10.8"
}

dependencies {
	compile "com.h2database:h2:${versionH2db}"
	compile "org.flywaydb:flyway-core:${versionFlyway}"
	compile "joda-time:joda-time:${versionJodaTime}"
	compile "com.google.guava:guava:${versionGuava}"
	compile "commons-pool:commons-pool:${versionCommonsPool}"
	compile "org.apache.commons:commons-dbcp2:${versionCommonsDbcp2}"
	compile "org.apache.commons:commons-lang3:${versionCommonsLang3}"
	compile "org.apache.commons:commons-math3:${versionCommonsMath3}"
	compile "org.apache.commons:commons-collections4:${versionCommonsCollections4}"
	compile "commons-codec:commons-codec:${versionCommonsCodec}"
	compile "org.apache.velocity:velocity:${versionVelocity}"
	compile "org.apache.poi:poi:${versionPoi}"
	compile "org.apache.poi:poi-ooxml:${versionPoi}"
	compile "com.ibm.icu:icu4j:${versionIcu4j}"
	compile "org.fluentd:fluent-logger:${versionFluentLogger}"
	compile "org.slf4j:slf4j-api:${versionSlf4j}"
	compile "org.slf4j:jcl-over-slf4j:${versionSlf4j}"
	compile "ch.qos.logback:logback-core:${versionLogback}"
	compile "ch.qos.logback:logback-classic:${versionLogback}"
	compile "org.mybatis:mybatis:${versionMyBatis}"
	compile "org.mybatis:mybatis-spring:${versionMyBatisSpring}"
	compile "com.mysema.querydsl:querydsl-sql:${versionQuerydsl}"
	compile "com.fasterxml.jackson.core:jackson-core:${versionJackson}"
	compile "com.fasterxml.jackson.core:jackson-annotations:${versionJackson}"
	compile "com.fasterxml.jackson.core:jackson-databind:${versionJackson}"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:${versionJackson}"
	compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${versionJackson}"
	compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${versionJackson}"
	compile "org.aspectj:aspectjrt:${versionAspectJ}"
	compile "org.aspectj:aspectjweaver:${versionAspectJ}"
	compile "aopalliance:aopalliance:${versionAopalliance}"
	compile "org.springframework:spring-core:${versionSpring}"
	compile "org.springframework:spring-beans:${versionSpring}"
	compile "org.springframework:spring-expression:${versionSpring}"
	compile "org.springframework:spring-aop:${versionSpring}"
	compile "org.springframework:spring-context:${versionSpring}"
	compile "org.springframework:spring-context-support:${versionSpring}"
	compile "org.springframework:spring-tx:${versionSpring}"
	compile "org.springframework:spring-jdbc:${versionSpring}"
	compile "org.springframework:spring-orm:${versionSpring}"
	compile "org.springframework:spring-oxm:${versionSpring}"
	compile "org.springframework:spring-jms:${versionSpring}"
	compile "org.springframework:spring-web:${versionSpring}"
	compile "org.springframework:spring-webmvc:${versionSpring}"
	compile "org.springframework.security:spring-security-core:${versionSpringSecurity}"
	compile "org.springframework.security:spring-security-acl:${versionSpringSecurity}"
	compile "org.springframework.security:spring-security-web:${versionSpringSecurity}"
	compile "org.springframework.security:spring-security-taglibs:${versionSpringSecurity}"
	compile "org.springframework.security:spring-security-config:${versionSpringSecurity}"
	compile "org.springframework.mobile:spring-mobile-device:${versionSpringMobile}"
	compile "org.springframework.data:spring-data-jdbc-core:${versionSpringDataJdbcCore}"
	compile "org.hibernate:hibernate-validator:${versionHibernateValidator}"
	compile "org.apache.tiles:tiles-core:${versionTiles}"
	compile "org.apache.tiles:tiles-api:${versionTiles}"
	compile "org.apache.tiles:tiles-servlet:${versionTiles}"
	compile "org.apache.tiles:tiles-jsp:${versionTiles}"
	compile "org.apache.tiles:tiles-el:${versionTiles}"
	compile "org.apache.tiles:tiles-extras:${versionTiles}"
	compile "javax.servlet:jstl:${versionJstl}"
	compile "javax.mail:mail:${versionJavaMail}"
	compile "javax.validation:validation-api:${versionJavaValidation}"
	compile "javax.el:javax.el-api:${versionJavaEl}"
	providedCompile "javax.servlet:javax.servlet-api:${versionJavaServlet}"
	providedCompile "javax.servlet.jsp:javax.servlet.jsp-api:${versionJavaJsp}"
	providedCompile "javax.jms:jms-api:${versionJavaJms}"
	providedCompile "javax.transaction:transaction-api:${versionJavaTransaction}"
	providedCompile "org.projectlombok:lombok:${versionLombok}"
	testCompile "org.springframework:spring-test:${versionSpring}"
	testCompile "junit:junit:${versionJunit}"
	testCompile "org.hamcrest:hamcrest-all:${versionHamcrest}"
	testCompile "org.mockito:mockito-all:${versionMockito}"
}

configurations {
	all*.exclude group: "commons-logging", module: "commons-logging"
	compileOnly
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

sourceSets.main {
	java {
		srcDir "src/generated/java"
		srcDir "goods/main/java"
		srcDir "foundation/main/java"
		srcDir "common/main/java"
	}
	resources {
		srcDir "src/main/java"
		srcDir "src/generated/java"
		srcDir "goods/main/java"
		srcDir "foundation/main/java"
		srcDir "foundation/main/resources"
		srcDir "common/main/java"
		srcDir "common/main/resources"
	}
}
sourceSets.test.resources.srcDir "src/test/java"

sourceSets.main.compileClasspath += [configurations.compileOnly]
sourceSets.test.compileClasspath += [configurations.compileOnly]
eclipse.classpath.plusConfigurations += [configurations.compileOnly]

eclipse.wtp.facet {
	facet name: "java", version: "1.7"
	facet name: "jst.web", version: "3.0"
}

war {
	classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
}

def messageDir = "src/main/resources/message"
task mergeformmessage {
	ant.concat(destfile: "${messageDir}/form/form.properties", encoding: "UTF-8", outputencoding: "UTF-8") {
		fileset(dir: "${messageDir}/form/") {
			include(name: "**.txt")
			exclude(name: "**.properties")
		}
	}
	ant.native2ascii(src: "${messageDir}/form/", dest: "${messageDir}/", includes: "form.properties", encoding: "UTF-8")
}

deliveryResources {
	from "src/main/deliveryResources"
	tokens "filter.properties"
	tokens { props ->
		def appdir = "/opt/" + project.name
		if (project.hasProperty("appendix")) {
			appdir = appdir + "/" + project.appendix
		}
		props.setProperty("filter.conf", "${appdir}/conf")
		props.setProperty("filter.log.dir", "${appdir}/log")
	}
}

task deliveryZip(type: Zip)
task deliveryTar(type: Tar) { compression = Compression.GZIP }

[deliveryZip, deliveryTar].each {
	it.baseName = project.name
	it.version = project.version
	if (project.hasProperty("appendix")) {
		it.appendix = project.appendix
	}
	it.classifier = "delivery"
	it.dependsOn assembleDelivery

	it.with(copySpec {
		into("app") {
			from("build/libs")
			include "*.war"
			rename {
				if (project.hasProperty("appendix")) {
					it.replace("-${version}", project.appendix)
				} else {
					it.replace("-${version}", "")
				}
			}
		}
		into("conf") {
			from(sourceSets.main.output.resourcesDir) {
				include "*.properties"
				["sqlapp", "foundation", "common", "log"].each { exclude "${it}.properties" }
				rename { "${it}.template" }
			}
		}
		into("log") {
		}
	})
}
